plugins {
    id 'cpp'
    id 'java-library'
    id 'google-test'
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
    id 'edu.wpi.first.NativeUtils' version '2025.3.0'
    id 'edu.wpi.first.GradleJni' version '1.1.0'
    id "edu.wpi.first.GradleRIO" version "2024.3.2"
    id 'edu.wpi.first.GradleVsCode' version '2.1.0'
    id 'com.diffplug.spotless' version '6.25.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/Mechanical-Advantage/AdvantageKit")
        credentials {
            username = "Mechanical-Advantage-Bot"
            password = "\u0067\u0068\u0070\u005f\u006e\u0056\u0051\u006a\u0055\u004f\u004c\u0061\u0079\u0066\u006e\u0078\u006e\u0037\u0051\u0049\u0054\u0042\u0032\u004c\u004a\u006d\u0055\u0070\u0073\u0031\u006d\u0037\u004c\u005a\u0030\u0076\u0062\u0070\u0063\u0051"
        }
    }
    mavenCentral()
}
if (project.hasProperty('releaseMode')) {
    wpilibRepositories.addAllReleaseRepositories(project)
} else {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

// Apply C++ configuration
apply from: 'config.gradle'

// Apply Java configuration
dependencies {
    def wpiVersion = "2025.+"
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    implementation "edu.wpi.first.cscore:cscore-java:${wpiVersion}"
    implementation "edu.wpi.first.cameraserver:cameraserver-java:${wpiVersion}"
    implementation "edu.wpi.first.ntcore:ntcore-java:${wpiVersion}"
    implementation "edu.wpi.first.wpilibj:wpilibj-java:${wpiVersion}"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:${wpiVersion}"
    implementation "edu.wpi.first.wpimath:wpimath-java:${wpiVersion}"
    implementation "edu.wpi.first.wpiunits:wpiunits-java:${wpiVersion}"
    implementation "edu.wpi.first.hal:hal-java:${wpiVersion}"

    implementation "org.ejml:ejml-simple:0.43.1"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.18.1"
    implementation "com.fasterxml.jackson.core:jackson-core:2.18.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.18.2"
    implementation 'edu.wpi.first.thirdparty.frc2024.opencv:opencv-java:4.8.0-4'

    implementation 'gov.nist.math:jama:1.0.3'

    testImplementation('org.junit.platform:junit-platform-launcher:1.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.3'
}

apply from: 'publish.gradle'

wrapper {
    gradleVersion '8.10.2'
}

spotless {
    enforceCheck false
    java {
        target fileTree('.') {
            include '**/*.java'
            exclude '**/build/**', '**/build-*/**'
        }
        toggleOffOn()
        googleJavaFormat()
        removeUnusedImports()
        importOrder()
        formatAnnotations()
        trimTrailingWhitespace()
        endWithNewline()
    }
    groovyGradle {
        target fileTree('.') {
            include '**/*.gradle'
            exclude '**/build/**', '**/build-*/**'
        }
        greclipse()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
    format 'xml', {
        target fileTree('.') {
            include '**/*.xml'
            exclude '**/build/**', '**/build-*/**'
        }
        eclipseWtp('xml')
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
    format 'misc', {
        target fileTree('.') {
            include '**/*.md', '**/.gitignore'
            exclude '**/build/**', '**/build-*/**'
        }
        trimTrailingWhitespace()
        indentWithSpaces(2)
        endWithNewline()
    }
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
